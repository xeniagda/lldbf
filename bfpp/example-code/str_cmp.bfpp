struct ChPair {
    ch1: Byte,
    ch2: Byte
}

struct LChPair {
    ch_pair: ChPair,
    marker: Byte,
}

// Layout:
// c1 = char from str 1
// c2 = char from str 2
// marker = 1
// (c1 c2 marker) ...

/// declare (P_marker, f_ch1, f_ch2, f_marker) at P_marker
declare (previous: LChPair, first: LChPair) at previous.marker

// Read s1
assume stable {
    to first.ch_pair.ch1 ,

    run dec32(first.ch_pair.ch1, first.ch_pair.ch2)

    to first.ch_pair.ch1
    unstable [
        /// declare (c_ch1, c_ch2, c_marker, n_ch1, n_ch2) at c_ch1
        declare (current: LChPair, next: LChPair) at current.ch_pair.ch1

        run add32(current.ch_pair.ch1, current.ch_pair.ch2)

        to current.marker +

        to next.ch_pair.ch1 ,
        run dec32(next.ch_pair.ch1, next.ch_pair.ch2)

        to next.ch_pair.ch1
    ]

    /// declare (l_marker, P_ch1) at P_ch1
    declare (last: LChPair, after: LChPair) at after.ch_pair.ch1

    to last.marker
    unstable [
        /// declare (p_marker, _, _, c_marker) at c_marker
        declare (previous: LChPair, current: LChPair) at current.marker
        to previous.marker
    ]
}

// Read s2
assume stable {
    to first.ch_pair.ch2 ,
    unstable [
        /// declare (c_ch2, c_marker, n_ch1, n_ch2) at c_ch2
        declare (current: LChPair, next: LChPair) at current.ch_pair.ch2

        // Set c_marker to 1 by clearing it and increasing it
        to current.marker [-] +

        to next.ch_pair.ch2 ,
    ]
    /// declare (l_marker, P_ch1, P_ch2) at P_ch2
    declare (last: LChPair, after: LChPair) at after.ch_pair.ch2
    to last.marker
    unstable [
        /// declare (p_marker, _, _, c_marker) at c_marker
        declare (previous: LChPair, current: LChPair) at current.marker
        to previous.marker
    ]
}

// Check if the strings match
to first.marker
unstable [
    /// declare (any_wrong, c_ch1, c_ch2, c_marker, _, _, n_marker) at c_marker
    declare (any_wrong: Byte, current: LChPair, next: LChPair) at current.marker

    // Subtract c_ch1 from c_ch2
    to current.ch_pair.ch1 [
        to current.ch_pair.ch2 -
        to current.ch_pair.ch1 -
    ]
    // If ch2 != 0, set any_wrong to 1
    to current.ch_pair.ch2 [
        to any_wrong [-] +
        to current.ch_pair.ch2 [-]
    ]

    // Move any_wrong to c_marker
    to current.marker [-]
    to any_wrong [
        to current.marker +
        to any_wrong -
    ]

    to next.marker
]

declare (any_wrong: Byte, current: LChPair) at current.marker
declare (isnt_wrong: Byte) with isnt_wrong at current.ch_pair.ch1

to isnt_wrong +
to any_wrong [

    // Print "diff"
    declare (_, tmp: Byte, ch: Byte) at _
    to ch [-]

    // d = 100
    run add100(ch, tmp)
    to ch .

    // i = 105, delta = 5
    run add5(ch, tmp)
    to ch .

    // f = 102, delta = -3
    run dec3(ch, tmp)
    to ch . .

    to isnt_wrong [-]

    to any_wrong [-]
]

to isnt_wrong [
    // Print "match"
    declare (ch: Byte, tmp: Byte) at ch
    to ch [-]

    // m = 109
    run add109(ch, tmp)
    to ch .

    // i = 97, delta = -12
    run dec12(ch, tmp)
    to ch .

    // t = 116, delta = 19
    run add19(ch, tmp)
    to ch .

    // c = 99, delta = -17
    run dec17(ch, tmp)
    to ch .

    // h = 104, delta = 5
    run add5(ch, tmp)
    to ch .

    to isnt_wrong [-]
]

// Newline
declare (ch: Byte, tmp: Byte) at ch
to ch [-]
run add10(ch, tmp)
to ch .
